def ciServer = 'TRAVIS'
def executingOnCI = "true".equals(System.getenv(ciServer))

// Since for CI we always do full clean builds, we don't want to pre-dex
// See http://tools.android.com/tech-docs/new-build-system/tips
subprojects {
    project.plugins.whenPluginAdded { plugin ->
        if ('com.android.build.gradle.AppPlugin'.equals(plugin.class.name) ||
                'com.android.build.gradle.LibraryPlugin'.equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = !executingOnCI
        }
    }
}

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        testInstrumentationRunner rootProject.ext.testInstrumentationRunner

        vectorDrawables.useSupportLibrary = true
        multiDexEnabled = true
    }

    dexOptions {
        preDexLibraries true
        javaMaxHeapSize project.JAVA_MAX_HEAP_SIZE
    }

    buildTypes {
        debug {
            minifyEnabled false
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'

            // Temporary workaround for Kotlin 1.2.50 and DataBinding bug. For more context see
            // https://issuetracker.google.com/issues/110198434 and
            // https://youtrack.jetbrains.com/issue/KT-24915#comment=27-2914947
            tasks.whenTaskAdded { task ->
                if (task.name == 'kaptReleaseKotlin') {
                    task.dependsOn dataBindingExportFeaturePackageIdsDebug
                }
            }
        }

        release {
            minifyEnabled rootProject.minifyEnabled
            useProguard rootProject.useProguard
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'

            // Temporary workaround for Kotlin 1.2.50 and DataBinding bug. For more context see
            // https://issuetracker.google.com/issues/110198434 and
            // https://youtrack.jetbrains.com/issue/KT-24915#comment=27-2914947
            tasks.whenTaskAdded { task ->
                if (task.name == 'kaptDebugKotlin') {
                    task.dependsOn dataBindingExportFeaturePackageIdsDebug
                }
            }
        }
    }

    dataBinding {
        enabled = true
        enabledForTests = true
    }

    sourceSets {
        main.java.srcDirs += rootProject.mainSrcDirs
        test.java.srcDirs += rootProject.testSrcDirs
        androidTest.java.srcDirs += rootProject.androidTestSrcDirs

        prod.java.srcDirs += rootProject.prodSrcDirs
        testProd.java.srcDirs += rootProject.prodTestSrcDirs
        androidTestProd.java.srcDirs += rootProject.prodAndroidTestSrcDirs

        mock.java.srcDirs += rootProject.mockSrcDirs
        testMock.java.srcDirs += rootProject.mockTestSrcDirs
        androidTestMock.java.srcDirs += rootProject.mockAndroidTestSrcDirs

    }

    lintOptions {
        htmlReport false
        abortOnError false
    }

    testOptions { // <-- You need this
        animationsDisabled = true
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }

        unitTests.all {
            // All the usual Gradle options.
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
            reports.html.enabled = true
            reports.junitXml.enabled = true
            maxParallelForks = 5
            forkEvery = 200
        }

    }

    flavorDimensions "app"

    productFlavors {
        prod {
            dimension "app"
        }
        mock {
            dimension "app"
        }
    }
}


configurations.all { configuration ->
    resolutionStrategy {
        force "com.android.support:support-v4:+",
                "com.android.support:appcompat-v7:+",
                "com.android.support:support-core-utils:+",
                "android.arch.lifecycle:runtime:+",
                "com.android.support:multidex:+"
    }
}



kotlin {
    experimental {
        coroutines "enable"
    }
}

kapt {
    useBuildCache = true
}
